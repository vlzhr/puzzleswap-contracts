{-# STDLIB_VERSION 6 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}



let usdnIdStr = "DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p"
let usdnId = base58'DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p'
let limitContract = "3PFB6LJyShsCKEA1AU1U1WLbDazqyj6ZL9b"
let tsnContract = "3PKz5jnJLohF2GjShQ7bG5H4BebdoA98zZ2"
let lendProxyContract = "3PGxMZtMnNT9pJCvXxXV4LzKqXsjvQ9XpfR"

func tryGetString(key: String) = {
  match getString(this, key) {
    case b:String => b
    case _ => ""
  }
}

func getAssetString(assetId: ByteVector|Unit) = {
  match assetId {
    case b:ByteVector => b.toBase58String()
    case _ => "WAVES"
  }
}

func getAssetBytes(assetIdStr: String) = {
  if (assetIdStr == "WAVES") then {unit} else {assetIdStr.fromBase58String()}
}

func getBalance(assetId: ByteVector|Unit) = {
  match assetId {
    case x: Unit => wavesBalance(this).available
    case _ => assetBalance(this, assetId.value())
  }
}


func swapLimit(params: String, t0Str: String, t1Str: String, balanceBefore: Int) = {
    func doLimit(asset: ByteVector|Unit, orderParams: String) = {
        let ord = orderParams.split(">")
        let (orderId, orderAmount) = (ord[0], ord[1])
        strict balanceActual = getBalance(t0Str.getAssetBytes()) - balanceBefore
        strict inv = invoke(Address(limitContract.fromBase58String()),"fulfillOrder",[orderId],[AttachedPayment(asset, balanceActual)]) 
        asset
    }
    let ordersLi = params.split(":")
    let t0 = getAssetBytes(t0Str)
    strict result = FOLD<10>(ordersLi, t0, doLimit)
    0
}

func swapLend(params: String, t0Str: String, t1Str: String, amount: Int) = {
    let arr = params.split("-")
    let (pool_address, asset, dir) = (arr[0], arr[1], arr[2])
    let proxyAdr = Address(lendProxyContract.fromBase58String())
    let functionName = if (dir == "0") then {
        "mintPL"
    } else {
        "redeemPL"
    }
    invoke(proxyAdr, functionName, [0], [AttachedPayment(getAssetBytes(t0Str), amount)])
}

func swapPepe(params: String, t0Str: String, t1Str: String, amount: Int) = {
  let arr = params.split("-")
  let (pool_address, functionTag) = (arr[0], arr[1])
  let swapAddress = Address(pool_address.fromBase58String())
  if (functionTag == "d") then {
    invoke(swapAddress, "deposit", [0], [AttachedPayment(getAssetBytes(t0Str), amount)])
  } else if (functionTag == "w") then {
    invoke(swapAddress, "withdraw", [t1Str, 0], [AttachedPayment(getAssetBytes(t0Str), amount)])
  } else if (functionTag == "s") then {
    invoke(swapAddress, "swap", [t1Str, 0], [AttachedPayment(getAssetBytes(t0Str), amount)])
  } else {
    throw("Unknown function tag for pepe swap")
  }
}

func swapInternal(i: Invocation, routesStr: String, minToReceive: Int, refKey: String) = {

  let shutdown = getBoolean("shutdown").valueOrElse(false)
  if (shutdown) then {throw("shutdown")}
  else if (i.payments.size() > 1) then {throw("only one payment should be attached")}
  else {

    # extract payment
    let asset0 = i.payments[0].assetId
    let asset0Str = asset0.getAssetString()
    let payAmount = i.payments[0].amount

    # add assets from swap string to affectedAssets list, if they are not present
    func f1(accum2: List[String], next: String) = {
      let swapElements = next.split(",")
      let t1 = if (indexOf(accum2, swapElements[2]) == unit) then {accum2 :+ swapElements[2]} else {accum2}
      let t2 = if (indexOf(accum2, swapElements[3]) == unit) then {t1 :+ swapElements[3]} else {t1}
      t2
    }

    # extract swap strings from route string
    func extractAssets(accum: List[String], next: String) = {
      let r1 = next.split("/")
      let data = r1[1]
      let swaps = data.split(";")
      accum ++ FOLD<20>(swaps, accum, f1)
    }

    # get balances before making swaps, taking into account payment amount
    func getBalances(accum: List[Int], next: String) = {
      let balance = if (next != asset0Str) then {getBalance(next.getAssetBytes())} else {getBalance(next.getAssetBytes()) - payAmount}
      accum :+ balance
    }

      # make routes list
    let routes = routesStr.split_4C("|")

    # get all assets used in route
    let affectedAssetsLi = FOLD<10>(routes, [], extractAssets)

    # get balances before for every used assets 
    strict balancesBefore = FOLD<20>(affectedAssetsLi, [], getBalances)

    func doSwap(amount: Int, swap: String) = {
      let swapLi = swap.split(",")
      let (dApp, dAppType, t0Str, t1Str) = (swapLi[0], swapLi[1], swapLi[2], swapLi[3])

      let t0 = getAssetBytes(t0Str)
      let t0Index = indexOf(affectedAssetsLi, t0Str).valueOrErrorMessage("Invalid asset d0001")
      let t0BalanceBefore = balancesBefore[t0Index]
      let amount0 = if( amount != 0 ) then amount else getBalance(t0) - t0BalanceBefore

      strict i1 = if (dAppType == "swopfi") then {
        invoke(Address(dApp.fromBase58String()),"exchange",[1],[AttachedPayment(t0, amount0)]) 
      } else if (dAppType == "puzzle") then {
        invoke(Address(dApp.fromBase58String()),"swap",[t1Str, 0],[AttachedPayment(t0, amount0)]) 
      } else if (dAppType == "wx") then {
        invoke(Address(base58'3P68zNiufsu1viZpu1aY3cdahRRKcvV5N93'), "swap", [1, t1Str, this.toString()], [AttachedPayment(t0, amount0)])
        # swapWX(t0Str, t1Str, amount0)
      } else if (dAppType == "limit") then {
          swapLimit(dApp, t0Str, t1Str, t0BalanceBefore)
      } else if (dAppType == "tsunami") then {
          invoke(Address(tsnContract.fromBase58String()), "swap", [t1Str, 0], [AttachedPayment(t0, amount0)])
      } else if (dAppType == "lend") then {
          swapLend(dApp, t0Str, t1Str, amount0)
      } else if (dAppType == "pepe") then {
          swapPepe(dApp, t0Str, t1Str, amount0)
      }
        else {
        throw("internal aggregator issue: unknow pool type")
      }

      0
    }

    func doRoute(accum: Int, route: String) = {
      let amountIn = route.split("/")[0].parseIntValue()
      let swaps = route.split("/")[1].split(";")
      strict swapsResult = FOLD<3>(swaps, amountIn, doSwap)
      accum + swapsResult
    }

  # find asset1
  let splittedRoute = routesStr.takeRight(50).split(",")
  let asset1Str =  splittedRoute[splittedRoute.size()-1]
  let asset1 = asset1Str.getAssetBytes()
  
  # do swaps
  strict swaps = FOLD<4>(routes, 0, doRoute)

  # calc result amount
  let totalOut = getBalance(asset1) - balancesBefore[indexOf(affectedAssetsLi, asset1Str).valueOrErrorMessage("Invalid asset d0002")]
  let feeAmount = fraction(totalOut, 29, 10000)
  let cleanTotalAmount = totalOut - feeAmount

  if (cleanTotalAmount < minToReceive) then {throw("amount to receive is too low. please try again with another amount or wait 1 minute. expected: " + minToReceive.toString() + ", real " + cleanTotalAmount.toString())} else

  strict usdnFee = if (asset1 == usdnId) then {
    feeAmount
  } else {
    let poolAddressStr = tryGetString("static_asset_"+asset1Str+"_puzzlePool")
    if (poolAddressStr == "") then {0} else {
      strict usdnBalanceBefore = getBalance(usdnId)
      strict convertedFee = invoke(addressFromStringValue(poolAddressStr),"swap",[usdnIdStr,0],[AttachedPayment(asset1, feeAmount)])
      strict usdnBalanceAfter = getBalance(usdnId)

      usdnBalanceAfter - usdnBalanceBefore
    }
  }

  let referralAddress = 
  if (refKey == "muna") then {Address(base58'3PGFHzVGT4NTigwCKP1NcwoXkodVZwvBuuU')}
  else if (refKey == "keeper") then {Address(base58'3PNgw6Db2TGUQbjnV7ekve6q126xBC1e7eM')} 
  else if (refKey == "tci") then {Address(base58'3P93R2GBkTGVehRPhytHr7sdxYww93jkbhm')} 
  else if (getString(this, "ref_"+refKey).valueOrElse("") != "") then {getStringValue(this, "ref_"+refKey).addressFromStringValue()}
  else {Address(base58'3PQxXXiQXpWsjDwQLYxyuoPiaLnVHjJguFo')}
  let refUsdnShare = if(refKey =="noref") then 0 else fraction(usdnFee, 2, 10)

  strict topUp = if (usdnFee != 0) then {
    invoke(Address(base58'3PKUxbZaSYfsR7wu2HaAgiirHYwAMupDrYW'),"topUpReward",[],[AttachedPayment(usdnId,usdnFee - refUsdnShare)])
  } else {
    unit
  }

  let amount0 = getBalance(asset0) - (if( asset0 == usdnId ) then refUsdnShare else 0) - balancesBefore[indexOf(affectedAssetsLi, asset0Str).valueOrErrorMessage("Invalid asset d0003")]
  let amount1 = cleanTotalAmount - if( asset1 == usdnId ) then refUsdnShare else 0

  [
    ScriptTransfer(i.caller, amount1, asset1)
  ]
  ++
  (if( refUsdnShare != 0 ) then [ScriptTransfer(referralAddress, refUsdnShare, usdnId)] else [])
  ++
  (if( amount0 != 0 ) then [ScriptTransfer(i.caller, amount0, asset0)] else [])
  ++
  # if asset1 is not added to puzzle fee pools
  (if( usdnFee == 0 ) then [ScriptTransfer(Address(base58'3P4kBiU4wr2yV1S5gMfu3MdkVvy7kxXHsKe'), feeAmount, asset1)] else [])
  
  }
}

@Callable(i)
func swap(routesStr: String, minToReceive: Int) = {
  swapInternal(i, routesStr, minToReceive, "noref")  
}


@Callable(i)
func swapWithReferral(routesStr: String, minToReceive: Int, refKey: String) = {
  swapInternal(i, routesStr, minToReceive, refKey)
}

@Callable(i)
func addTokenPuzzlePool(assetIdStr: String, poolAddressStr: String) = {
  if (i.caller != Address(base58'3PMcMiMEs6w56NRGacksXtFG5zS7doE9fpL')) then {throw("admin only")}
  else {
    [
      StringEntry("static_asset_"+assetIdStr+"_puzzlePool", poolAddressStr)
    ]
  }
}

@Callable(i)
func addReferralKey(refKey: String, addressStr: String) = {
  if (i.caller != Address(base58'3PMcMiMEs6w56NRGacksXtFG5zS7doE9fpL')) then {throw("admin only")}
  else {
    [
      StringEntry("ref_"+refKey, addressStr)
    ]
  }
}

@Callable(i)
func shutdown(val: Boolean) = {
  if (i.caller != Address(base58'3PMcMiMEs6w56NRGacksXtFG5zS7doE9fpL')) then {throw("wl only")}
  else {
    [
      BooleanEntry("shutdown", val)
    ]
  }
}



@Verifier(tx)
func verify() = sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
